{-# LANGUAGE BangPatterns #-}
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes #-}
{-# LANGUAGE ViewPatterns #-}
module Clash.Cores.Floating.Xilinx.Internal
where

import Clash.Explicit.Prelude

import Clash.Annotations.Primitive

import Clash.Cores.Floating.Xilinx.TH

-- | Customize Xilinx floating point IP.
--
-- These customizations influence how the IP will be synthesized. It does not
-- affect behavior.
data FloatingConfig = FloatingConfig
  { floatingArchOpt :: !FloatingArchOpt
    -- ^ Architecture optimisations
  , floatingDspUsage :: !FloatingDspUsage
    -- ^ DSP slice usage
  , floatingBMemUsage :: !Bool
    -- ^ Use block memory for exponential function
  } deriving (Eq, Show)

-- | Architecture optimizations.
--
-- For those operations that support different architecture optimizations.
data FloatingArchOpt
  = SpeedArch
    -- ^ Speed-optimized architecture
  | LatencyArch
    -- ^ Low latency architecture
  deriving (Eq, Show)

-- | DSP slice usage.
--
-- For those operations that can use DSP slices. Not all such operations support
-- all variants.
data FloatingDspUsage
  = NoDspUsage
  | MediumDspUsage
  | FullDspUsage
  | MaxDspUsage
  deriving (Eq, Show)

-- | NaN as generated by Xilinx
--
-- Quiet NaN, no payload, positive
xilinxNaN :: Float
xilinxNaN = unpack 0x7FC00000

conditionFloat
  :: Float
  -> Float
conditionFloat x
  | isNaN x          = xilinxNaN
  | isDenormalized x = if x > 0 then 0 else -0
  | otherwise        = x

conditionFloatF
  :: Functor f
  => f Float
  -> f Float
conditionFloatF = fmap conditionFloat

addFloat#
  :: forall d dom n
   . ( KnownDomain dom
     , KnownNat d
     )
  => FloatingConfig
  -> Clock dom
  -> Enable dom
  -> DSignal dom n Float
  -> DSignal dom n Float
  -> DSignal dom (n + d) Float
addFloat# !_ clk en (conditionFloatF -> x) (conditionFloatF -> y) =
  let d = delayI (deepErrorX "Initial values of addFloat undefined") en clk
  in d . conditionFloatF $ x + y
{-# NOINLINE addFloat# #-}
{-# ANN addFloat#
      (InlinePrimitive [VHDL] $ vhdlBinaryPrim
         "Clash.Cores.Floating.Xilinx.Internal.addFloat#"
         "addFloat"
         "Clash.Cores.Floating.Xilinx.BlackBoxes.addFloatTclTF"
      ) #-}

subFloat#
  :: forall d dom n
   . ( KnownDomain dom
     , KnownNat d
     )
  => FloatingConfig
  -> Clock dom
  -> Enable dom
  -> DSignal dom n Float
  -> DSignal dom n Float
  -> DSignal dom (n + d) Float
subFloat# !_ clk en (conditionFloatF -> x) (conditionFloatF -> y) =
  let d = delayI (deepErrorX "Initial values of subFloat undefined") en clk
  in d . conditionFloatF $ x - y
{-# NOINLINE subFloat# #-}
{-# ANN subFloat#
      (InlinePrimitive [VHDL] $ vhdlBinaryPrim
         "Clash.Cores.Floating.Xilinx.Internal.subFloat#"
         "subFloat"
         "Clash.Cores.Floating.Xilinx.BlackBoxes.subFloatTclTF"
      ) #-}

mulFloat#
  :: forall d dom n
   . ( KnownDomain dom
     , KnownNat d
     )
  => FloatingConfig
  -> Clock dom
  -> Enable dom
  -> DSignal dom n Float
  -> DSignal dom n Float
  -> DSignal dom (n + d) Float
mulFloat# !_ clk en (conditionFloatF -> x) (conditionFloatF -> y) =
  let d = delayI (deepErrorX "Initial values of mulFloat undefined") en clk
  in d . conditionFloatF $ x * y
{-# NOINLINE mulFloat# #-}
{-# ANN mulFloat#
      (InlinePrimitive [VHDL] $ vhdlBinaryPrim
         "Clash.Cores.Floating.Xilinx.Internal.mulFloat#"
         "mulFloat"
         "Clash.Cores.Floating.Xilinx.BlackBoxes.mulFloatTclTF"
      ) #-}

divFloat#
  :: forall d dom n
   . ( KnownDomain dom
     , KnownNat d
     )
  => FloatingConfig
  -> Clock dom
  -> Enable dom
  -> DSignal dom n Float
  -> DSignal dom n Float
  -> DSignal dom (n + d) Float
divFloat# !_ clk en (conditionFloatF -> x) (conditionFloatF -> y) =
  let d = delayI (deepErrorX "Initial values of divFloat undefined") en clk
  in d . conditionFloatF $ x / y
{-# NOINLINE divFloat# #-}
{-# ANN divFloat#
      (InlinePrimitive [VHDL] $ vhdlBinaryPrim
         "Clash.Cores.Floating.Xilinx.Internal.divFloat#"
         "divFloat"
         "Clash.Cores.Floating.Xilinx.BlackBoxes.divFloatTclTF"
      ) #-}

expFloat#
  :: forall d dom n
   . ( KnownDomain dom
     , KnownNat d
     )
  => FloatingConfig
  -> Clock dom
  -> Enable dom
  -> DSignal dom n Float
  -> DSignal dom (n + d) Float
expFloat# !_ clk en (conditionFloatF -> x) =
  let d = delayI (deepErrorX "Initial values of expFloat undefined") en clk
  in d . conditionFloatF $ exp <$> x
{-# NOINLINE expFloat# #-}
{-# ANN expFloat#
      (InlinePrimitive [VHDL] $ vhdlUnaryPrim
         "Clash.Cores.Floating.Xilinx.Internal.expFloat#"
         "expFloat"
         "Clash.Cores.Floating.Xilinx.BlackBoxes.expFloatTclTF"
      ) #-}
